// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:

    // handle whether to work with RAM, Screen, Keyboard based on last digits of kb
    // if 0-16384 -> 00 or 01 -> RAM
    // if 16384-24576 -> 10 -> screen
    // if 24576+ -> 11 -> keyboard
    // first, secondRAM is or'd anyways so doesn't matter
    DMux4Way(in=load, sel=address[13..14], a=firstRAM, b=secondRAM, c=screen, d=keyboard);
    Or(a=firstRAM, b=secondRAM, out=loadRAM);

    // handle RAM, only take first 14 inputs
    RAM16K(in=in, load=loadRAM, address=address[0..13], out=ramOut);

    // handle screen, but want to subtract 16384 for screen by only taking first 13 ipnuts
    Screen(in=in, load=screen, address=address[0..12], out=screenOut);

    // handle keyboard output
    Keyboard(out=keyboardOut);

    // consolidate output from RAM, Screen, Keyboard
    Mux4Way16(a=ramOut, b=ramOut, c=screenOut, d=keyboardOut, sel=address[13..14], out=out);

}